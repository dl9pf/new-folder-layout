# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# Let us add layer-specific bbappends which are only applied when that
# layer is included in our configuration -
# e.g. only add layers if a bsp is present.
# This is based on the BBFILE_COLLECTIONS name of the layer.
# We use either the BBFILE_COLLECTIONS name of the layer directly (meta-ti)
BBFILES += "${@' '.join('${LAYERDIR}/%s/recipes*/*/*.bbappend' % layer \
               for layer in BBFILE_COLLECTIONS.split())}"
BBFILES += "${@' '.join('${LAYERDIR}/%s/recipes*/*/*.bb' % layer \
               for layer in BBFILE_COLLECTIONS.split())}"
# or we prepend it with meta-  (e.g. rpi, fsl*)
BBFILES += "${@' '.join('${LAYERDIR}/meta-%s/recipes*/*/*.bbappend' % layer \
               for layer in BBFILE_COLLECTIONS.split())}"
BBFILES += "${@' '.join('${LAYERDIR}/meta-%s/recipes*/*/*.bb' % layer \
               for layer in BBFILE_COLLECTIONS.split())}"

# We have recipes-* directories, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
	${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "speech-framework"
BBFILE_PATTERN_speech-framework = "^${LAYERDIR}/"
BBFILE_PRIORITY_speech-framework = "8"

LAYERSERIES_COMPAT_speech-framework = "dunfell"
